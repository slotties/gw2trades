apply plugin: 'maven'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "http://repo.spring.io/release" }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE'
        classpath 'com.eriwen:gradle-css-plugin:1.11.1'
        classpath 'com.eriwen:gradle-js-plugin:1.12.1'
    }
}

apply plugin: 'css'
css.source {
    dev {
        css {
            srcDir 'src/main/resources/static/less'
            include "*.less"
        }
    }
}
lesscss {
    source = css.source.dev.css.files
    dest = 'src/main/resources/static/css'
}
minifyCss {
    source = lesscss
    dest = "${buildDir}/resources/main/static/css/styles.css"
    yuicompressor {
        lineBreakPos = -1
    }
}

apply plugin: "com.eriwen.gradle.js"
javascript.source {
    dev {
        js {
            srcDir "src/main/resources/static/js"
            include "*.js"
            exclude "*.min.js"
        }
    }
}
jshint {
    tasks.jshint.source = javascript.source.dev.js.files
    tasks.jshint.dest = file("${buildDir}/jshint.out")
    tasks.jshint.reporter = 'checkstyle'
    tasks.jshint.ignoreExitCode = false
    jshint.options = [expr: "true", unused: "true"]
}
minifyJs {
    source = javascript.source.dev.js.files
    dest = file("${buildDir}/resources/main/static/js/history.js")
    sourceMap = file("${buildDir}/resources/main/static/js/history.sourcemap.json")
    closure {
        warningLevel = 'QUIET'
    }
}

apply plugin: 'spring-boot'

configurations {
    // We use log4j2 over commons-logging/slf4j/logback:
    compile.exclude module: 'spring-boot-starter-logging'
}

dependencies {
    compile project(':repository')

    compile 'com.fasterxml.jackson.core:jackson-core:2.4.6'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.6'
    compile 'org.apache.logging.log4j:log4j-api:2.4.1'
    compile 'org.apache.logging.log4j:log4j-core:2.4.1'
    compile 'org.springframework.boot:spring-boot-starter:1.2.7.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-velocity'
    compile 'org.springframework.boot:spring-boot-starter-log4j2'
    compile 'org.springframework.boot:spring-boot-starter-actuator'

    testCompile "org.codehaus.groovy:groovy-all:2.4.1"
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testRuntime 'cglib:cglib-nodep:3.1'
}

tasks.jar.dependsOn(tasks.jshint)
tasks.jar.dependsOn(minifyCss)
tasks.jar.dependsOn(minifyJs)